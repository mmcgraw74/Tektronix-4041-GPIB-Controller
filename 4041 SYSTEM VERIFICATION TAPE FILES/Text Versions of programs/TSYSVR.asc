100! This material, including all material APPENDed, LOADed, or
110! CALLed by this program, may not be copied or otherwise
120! reproduced without consent of Tektronix, Inc.
130! COPYRIGNT (c) 1982  TEKTRONIX, INC.   ALL RIGHTS RESERVED
140!
150!  ***********************
160!  * SYSTEM VERIFICATION *
170!  ***********************
180!
190!    V2.8       1 Jun 84
200!
210! Initialization
220!
230Disa abor
240Comp all
250On erro(700 to 999)then gosu globerr
260Dim consl$ to 275
270Dim tapevol$ to 10
280Dim curmod$ to 7,mod$ to 7
290Inte dsns,cnip,winh,nofile,fopn,invt,short,lstcmd,cmnd
300Tapevol$=ask$("volume")
310Dsns=52! Undefined sub or var name
320Cnip=842! Cartridge not in place
330Winh=843! Tape is write inhibited
340Nofile=853! File does not exist
350Fopn=857! A file is not open
360Invt=864! Invalid tape in system
370Short=861! Invalid tape, not the tape modified
380Lstcmd=0
390Curmod$=""
400Mod$=""
410Consl$=""
420One:!
430!
440! Append in the utility subprograms
450!
460Appe"proced"
470Appe"link"
480!
490On abor then gosu abrt1
500Enab abor
510Open#2:"prin:"
520Prin#2:"RESPOND TO QUESTIONS"
530Prin#2:"0 FOR NO,  1 FOR YES"
540Prin#2:"FOLLOW W/ <PROCEED>"
550Prin#2:"PRESS <PROCEED> WHEN"
560Prin#2:"A REQUESTED ACTION"
570Prin#2:"HAS BEEN COMPLETED."
580Prin#2:"<ABORT> EXITS A TEST"
590Prin#2:
600Clos 2
610Setcon:!
620Consl$=ask$("console")
630If seg$(consl$,1,4)<>"FRTP"then goto prtmsg
640Set cons"frtp(view=2,rate=1):"
650Prtmsg:!
660On abor then gosu skip_pr! Skip primary tests
670Enab abor
680Prin"SYSVER V2.8  (V";seg$(ask$("id"),19,25);")"
690Wait 3
700Deftst:! Default tests
710Dele line one+1 to deftst-1
720Call link("disp")
730Call disp
740Prin#"frtp:":
750Call link("keypad")
760Call keypad
770Prin#"frtp:":
780Call link("printer")
790Call printer
800Prin#"frtp:":
810Call link("tape")
820Call tape
830Prin#"frtp:":
840!
850! Command mode
860!
870Get_cmd:!
880On abor then gosu abrt! <ABORT> means terminate
890Enab abor
900Prin#"FRTP:":! Clear front panel
910Comp 
920Inpu prom"COMMAND(99=HELP)? ":cmnd
930If cmnd=0 then goto quit
940If cmnd=1 then mod$="disp"
950If cmnd=2 then mod$="keypad"
960If cmnd=3 then mod$="printer"
970If cmnd=4 then mod$="tape"
980If cmnd=10 then goto deftst
990If cmnd=11 or cmnd=12 then mod$="io"
1000If cmnd=13 then mod$="timer"
1010If cmnd=14 then mod$="pdkybd"
1020If cmnd=15 or cmnd=16 then mod$="gpib"
1030If cmnd=17 then mod$="gpiblb"
1040If cmnd=18 then mod$="gpibcom"
1050If cmnd=19 then init self
1060If cmnd=20 then mod$="paus"
1070If cmnd=21 then mod$="scsi"
1080If cmnd=31 then mod$="tpcal"
1090If cmnd=32 then mod$="ptrcal"
1100If cmnd=41 then mod$="frmat"
1110If cmnd=42 then mod$="tstptn"
1120If cmnd=43 then mod$="chkopt"
1130If cmnd=44 then mod$="opt2"
1140If cmnd=99 then mod$="help"
1150If mod$=""then goto get_cmd
1160!
1170If curmod$=mod$ then goto perform else call link(mod$)
1180Perform:! Do test requested
1190If cmnd=1 then call disp
1200If cmnd=2 then call keypad
1210If cmnd=3 then call printer
1220If cmnd=4 then call tape
1230If cmnd=11 then call stdio
1240If cmnd=12 then call optio
1250If cmnd=13 then call timer
1260If cmnd=14 then call pdkybd
1270If cmnd=15 then call sdgpib
1280If cmnd=16 then call opgpib
1290If cmnd=17 then call gpiblb
1300If cmnd=18 then call gpibcom
1310If cmnd=20 then call paus
1320If cmnd=21 then call scsi
1330If cmnd=31 then call tpcal
1340If cmnd=32 then call ptrcal
1350If cmnd=41 then call frmat
1360If cmnd=42 then call tstptn
1370If cmnd=43 then call chkopt
1380If cmnd=44 then call opt2
1390If cmnd=99 then call help
1400Mod$=""
1410Goto get_cmd
1420Quit:!
1430Clos all
1440Dism 
1450Prin"VERIFICATION EXIT"
1460Goto done
1470!
1480Skip_pr:! Skip group 1 tests
1490Bran get_cmd
1500Abrt:! Terminate verification
1510Bran quit
1520Abrt1:!
1530Bran setcon
1540Globerr:!
1550Prin#"frtp(view=5):":val(ask$("error"));" ERROR DETECTED"
1560Curmod$=""
1570Bran get_cmd
1580Done:!
1590End
