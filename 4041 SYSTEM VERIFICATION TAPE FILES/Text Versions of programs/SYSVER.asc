  130   ! COPYRIGNT (c) 1982  TEKTRONIX, INC.   ALL RIGHTS RESERVED
  140   !  Formatted version in "TSYSVR".  Formatted may not run on 32K.
  230     Disable abort
  240     Compress all
  250     On error(700 to 999) then gosub globerr
  260     Dim consl$ to 275
  270     Dim tapevol$ to 10
  280     Dim curmod$ to 7,mod$ to 7
  290     Integer dsns,cnip,winh,nofile,fopn,invt,short,lstcmd,cmnd
  300     Tapevol$=ask$("volume")
  310     Dsns=52
  320     Cnip=842
  330     Winh=843
  340     Nofile=853
  350     Fopn=857
  360     Invt=864
  370     Short=861
  380     Lstcmd=0
  390     Curmod$=""
  400     Mod$=""
  410     Consl$=""
  460 One:     append "proced"
  470     Append "link"
  490     On abort then gosub abrt1
  500     Enable abort
  510     Open #2:"prin:"
  520     Print #2:"RESPOND TO QUESTIONS"
  530     Print #2:"0 FOR NO,  1 FOR YES"
  540     Print #2:"FOLLOW W/ <PROCEED>"
  550     Print #2:"PRESS <PROCEED> WHEN"
  560     Print #2:"A REQUESTED ACTION"
  570     Print #2:"HAS BEEN COMPLETED."
  580     Print #2:"<ABORT> EXITS A TEST"
  590     Print #2:
  600     Close 2
  620 Setcon:     consl$=ask$("console")
  630     If seg$(consl$,1,4)<>"FRTP" then goto prtmsg
  640     Set console "frtp(view=2,rate=1):"
  660 Prtmsg:     on abort then gosub skip_pr
  670     Enable abort
  680     Print "SYSVER V2.8  (V";seg$(ask$("id"),19,25);")"
  690     Wait 3
  710 Deftst:     delete line one+1 to deftst-1
  720     Call link("disp")
  730     Call disp
  740     Print #"frtp:":
  750     Call link("keypad")
  760     Call keypad
  770     Print #"frtp:":
  780     Call link("printer")
  790     Call printer
  800     Print #"frtp:":
  810     Call link("tape")
  820     Call tape
  830     Print #"frtp:":
  880 Get_cmd:     on abort then gosub abrt
  890     Enable abort
  900     Print #"FRTP:":
  910     Compress
  920     Input prompt "COMMAND(99=HELP)? ":cmnd
  930     If cmnd=0 then goto quit
  940     If cmnd=1 then mod$="disp"
  950     If cmnd=2 then mod$="keypad"
  960     If cmnd=3 then mod$="printer"
  970     If cmnd=4 then mod$="tape"
  980     If cmnd=10 then goto deftst
  990     If cmnd=11 or cmnd=12 then mod$="io"
 1000     If cmnd=13 then mod$="timer"
 1010     If cmnd=14 then mod$="pdkybd"
 1020     If cmnd=15 or cmnd=16 then mod$="gpib"
 1030     If cmnd=17 then mod$="gpiblb"
 1040     If cmnd=18 then mod$="gpibcom"
 1050     If cmnd=19 then init selftest
 1060     If cmnd=20 then mod$="paus"
 1070     If cmnd=21 then mod$="scsi"
 1080     If cmnd=31 then mod$="tpcal"
 1090     If cmnd=32 then mod$="ptrcal"
 1100     If cmnd=41 then mod$="frmat"
 1110     If cmnd=42 then mod$="tstptn"
 1120     If cmnd=43 then mod$="chkopt"
 1130     If cmnd=44 then mod$="opt2"
 1140     If cmnd=99 then mod$="help"
 1150     If mod$="" then goto get_cmd
 1170     If curmod$=mod$ then goto perform else call link(mod$)
 1190 Perform:     if cmnd=1 then call disp
 1200     If cmnd=2 then call keypad
 1210     If cmnd=3 then call printer
 1220     If cmnd=4 then call tape
 1230     If cmnd=11 then call stdio
 1240     If cmnd=12 then call optio
 1250     If cmnd=13 then call timer
 1260     If cmnd=14 then call pdkybd
 1270     If cmnd=15 then call sdgpib
 1280     If cmnd=16 then call opgpib
 1290     If cmnd=17 then call gpiblb
 1300     If cmnd=18 then call gpibcom
 1310     If cmnd=20 then call paus
 1320     If cmnd=21 then call scsi
 1330     If cmnd=31 then call tpcal
 1340     If cmnd=32 then call ptrcal
 1350     If cmnd=41 then call frmat
 1360     If cmnd=42 then call tstptn
 1370     If cmnd=43 then call chkopt
 1380     If cmnd=44 then call opt2
 1390     If cmnd=99 then call help
 1400     Mod$=""
 1410     Goto get_cmd
 1430 Quit:     close all
 1440     Dismount
 1450     Print "VERIFICATION EXIT"
 1460     Goto done
 1490 Skip_pr:     branch get_cmd
 1510 Abrt:     branch quit
 1530 Abrt1:     branch setcon
 1550 Globerr:     print #"frtp(view=5):":val(ask$("error"));" ERROR DETECTED^G"
 1560     Curmod$=""
 1570     Branch get_cmd
 1590 Done:     end
