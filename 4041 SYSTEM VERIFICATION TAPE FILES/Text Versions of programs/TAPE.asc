  100 Sub tape local hdr$,curtape$,i,shrt,count,j,olderr,newerr,buff$,chk$,temp$,size,rand$,done,abrt,next_i,next_j,nderr,cnip_err,fopn_err,tape_qst,tape_in,rdfile,shrt_tst,nofl_err,notape,ok_open,del_tp,winherr,goback,updaterr,fnferr,no_form,writ_prt,rept
  110   !
  120   !  *************
  130   !  * TAPE TEST *
  140   !  *************
  150   !
  160     On abort then gosub abrt
  170     On error(840 to 865) then gosub nderr
  180     On error(849) then gosub no_form
  190     On error(cnip) then gosub cnip_err
  200     On error(fopn) then gosub fopn_err
  210     Dim hdr$ to 20,curtape$ to 10,buff$ to 386,chk$ to 386,temp$ to 20,rand$ to 1
  220     Integer i,shrt,count,j,olderr,newerr
  230     Olderr=-1
  240     I=0
  250     Hdr$="4041 SYSVER:  TPTEST"
  260     Print "TAPE TEST"
  270   !
  280     If not(proced("REPORT SOFT ERRORS?")) then goto tape_qst
  290     I=1
  300     Append "softerr"
  310 Tape_qst:   !
  320     If not(proced("ANOTHER TAPE? ")) then goto tape_in
  330     J=proced("INSTALL TAPE.")
  340 Tape_in:   !
  350     If (i) then olderr=softerr
  360     If (i) then delete line softerr
  370     Compress all
  380     If (proced("SHORT TEST? ")) then goto shrt_tst
  390     Open #3:"tptest(open=replace)",temp$
  400     Size=valc(temp$,10)
  410     Size=(size*2)-280
  420     Count=(size div 384)-5
  430     If count<=0 then count=count+5
  440     Shrt=0
  450     Close 3
  460     Goto rdfile
  470 Shrt_tst:   !
  480     Count=1
  490     Shrt=1
  500 Rdfile:   !
  510     Print "READING DIRECTORY"
  520     On error(nofile) then gosub nofl_err
  530     Open #3:"TAPE:TPTEST(OPEN=OLD)"
  540     On error(cnip) then gosub notape
  550     Print "CHECKING HEADER"
  560     If eof(3) then goto ok_open
  570     Input #3:chk$
  580     If chk$=hdr$ then goto ok_open
  590     On error(winh) then gosub writ_prt
  600     If proced("DELETE TPTEST?") then close 3 else goto done
  610 Del_tp:   !
  620     Delete file "TPTEST"
  630 Ok_open:   !
  640   !
  650   ! Open file and write test pattern to it
  660   !
  670     Close 3
  680     On error(winh) then gosub winherr
  690     If (shrt) then open #3:"tptest(open=rep,size=510)" else open #3:"tptest(open=rep,size="&str$(size)&")"
  700     Print #3:hdr$
  710     Print "WRITING TO TAPE"
  720 Goback:   !
  730     Rand$=chr$(round(rnd(1)*100))
  740     If (asc(rand$)>0 and asc(rand$)<31) or asc(rand$)=127 then goto goback
  750     Buff$=rand$
  760     For i=1 to count
  770       For j=1 to 384
  780         Buff$=buff$&chr$([AAh])
  790         Next j
  800       Print #3:buff$
  810       If i<>count then buff$=chr$([AAh])
  820       Next i
  830     Print "UPDATING DIRECTORY"
  840     On error(cnip) then gosub updaterr
  850     Close 3
  860     On error(cnip) then gosub notape
  870   !
  880   ! Open file and read it in
  890   !
  900     On error(nofile) then gosub fnferr
  910     Compress
  920     Open #3:"TAPE:TPTEST(OPEN=OLD)"
  930     If eof(3) then goto badtpts
  940     Input #3:chk$
  950     If chk$=hdr$ then goto head_ok
  960 Badtpts:   ! Header is wrong
  970     Print "TAPE TEST INVALID"
  980     Goto done
  990   !
 1000 Head_ok:   !
 1010     Print "READING FROM TAPE"
 1020     For i=1 to count
 1030       Input #3:chk$
 1040       If i<>1 then goto normal
 1050       If chk$=rand$&seg$(buff$,2,384) then goto next_i else goto repterr
 1060 Normal:   !
 1070       If chk$=buff$ then goto next_i else goto repterr
 1080 Next_i:   !
 1090       Next i
 1100     Goto done
 1110   !
 1120   ! Data disagrees.  Report error.
 1130   !
 1140     Open #2:"prin:"
 1150     Print #2:"DATA DISAGREES"
 1160     Print "DATA DISAGREES"
 1170     For i=1 to count
 1180       For j=1 to 384
 1190         If j<>1 and i<>1 then goto next_j
 1200         If seg$(chk$,1,1)<>rand$ then exit to rept
 1210 Next_j:   !
 1220         If seg$(chk$,i*j,1)<>chr$([AAh]) then exit to rept
 1230         Next j
 1240       Next i
 1250 Rept:   !
 1260     Print #2:"POSITION IN ERROR-";i*j
 1270     Print "POSITION IN ERROR-";i*j
 1280   !
 1290 Done:   !
 1300     Close 2,3
 1310     If olderr=-1 then goto bye
 1320     Append "softerr"
 1330     Delete file "tptest"
 1340     Newerr=softerr
 1350     Print newerr-olderr;" SOFT ERRORS FOUND"
 1360     Delete line softerr
 1370 Bye:   !
 1380     Curtape$=ask$("volume")
 1390     If tapevol$=curtape$ then goto prexmsg
 1400     J=proced("INSTALL SYSVER TAPE.")
 1410 Prexmsg:   !
 1420     Print "TAPE TEST EXIT"
 1430     Return
 1440   !
 1450 Abrt:   !
 1460     Branch done
 1470 Cnip_err:   !
 1480     If proced("INSERT TAPE.") then retry else branch done
 1490 Notape:   !
 1500     Dismount
 1510     Print "TAPE TEST ABORTED"
 1520     Branch done
 1530 Updaterr:   !
 1540     If proced("REINSTALL TAPE.") then retry else branch done
 1550     On error(cnip) then gosub tryagain
 1560     On error(short) then gosub tryagain
 1570     Branch done
 1580 Tryagain:   !
 1590     Retry
 1600 Nofl_err:   ! File does not exist (But that's OK)
 1610     Branch ok_open
 1620 Fnferr:   ! File does not exist (But that's NOT OK)
 1630     Branch badtpts
 1640 Winherr:   !
 1650     If proced("WRITE PROTECTED, CONTINUE? ") then branch rdfile else branch done
 1660 Fopn_err:   ! File not there to delete
 1670     Advance
 1680 Nderr:   ! Non-descript tape error
 1690     J=val(ask$("error"))
 1700     Print "TAPE ERROR ";j
 1710     Branch done
 1720 Writ_prt:   !
 1730     If proced("WRITE PROTECTED, CONTINUE? ") then branch del_tp else branch done
 1740 No_form:   !
 1750     If proced("USE FORMATTED TAPE.") then retry else branch done
 1760     End
