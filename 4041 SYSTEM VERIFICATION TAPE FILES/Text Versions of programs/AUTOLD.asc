
  100   ! This material may not be copied or otherwise
  110   ! reproduced without consent of Tektronix, Inc.
  120   ! COPYRIGHT (c) 1982  TEKTRONIX, INC.   ALL RIGHTS RESERVED
  130   !
  140   !  ***************
  150   !  * AUTOLD FILE *
  160   !  ***************
  170   !
  180     Compress all
  190     Dim in$ to 1
  200     If not(pwrslf) then goto lod_prg
  210 Redo:   !
  220     Input prompt "ERROR(1-OK,0-EXIT)?^G":in$
  230     If in$<>"" and in$<>"1" and in$<>"0" then goto redo
  240     If in$="0" then goto end_prg
  250   !
  260   ! Load SYSVER or TERMIN
  270   !
  280 Lod_prg:   !
  290     Compress all
  300 Goback:   !
  302     Input prompt "CONSOLE TO COMM(1-YES,0-NO)?":in$
  303     If in$<>"0" and in$<>"1" then goto goback
  304     If in$="0" then goto goterm
  305     Set driver "COMM0(BAUD=9600,FLA=BID,EDIT=STORAGE):"
  306     Print "CONSOLE >> COMM0"
  307     Set console "COMM0(ECHO=YES):"
  308     Goto end_prg
  309 Goterm:   !
  310     Input prompt "TERMINAL CONFIG(1-YES,0-NO)?":in$
  320     If in$<>"0" and in$<>"1" then goto goterm
  330     If in$="0" then load "sysver" else load "termin"
  340 End_prg:   !
  350     End
  400 Function pwrslf local res_n,self$,labl$,flag,n_lc,n_eq,nt
  410   !
  420   !  **************************
  430   !  * POWERUP SELFTEST CHECK *
  440   !  **************************
  450   !
  460   ! This subroutine takes the results of the BASIC function
  470   ! ASK$("selftest") and checks to see if the results are non-zero
  480   ! and if they are, it displays the results to both the console and
  490   ! the printer.  If there are no exceptional results then there
  500   ! will be no message printed.
  510   !
  520   ! Initialization
  530   !
  580     Dim labl$ to 10
  590     Dim self$ to 128
  600     Flag=0 ! Initialize FLAG to false
  700   !
  710   ! Get results of powerup SELFTEST
  720   !
  730     Self$=ask$("selftest")
  740     Nt=0
  745     N_lc=0
  750 Nex_test:   !
  755     Nt=nt+1
  760     N_eq=posn(self$,"=",1,nt)
  770     Labl$=seg$(self$,n_lc+1,n_eq-n_lc-1)
  780     Res_n=valc(self$,n_eq)
  840   !
  850   ! If result is zero then test passes
  860   !
  870     If res_n=0 then goto test_ok
  880     If not(flag) then call print1("ERROR")
  890     Flag=1 ! Set FLAG to true
  900   !
  910   ! Print results of test in hexadecimal
  920   !
  930     Print using "5a,'=',8h" #"prin:":labl$,res_n
  940     Print using "5A,'=',8h":labl$,res_n
  945   !
  950 Test_ok:   ! Go get result of next test
  960     N_lc=posn(self$,",",1,nt)
  962     If n_lc=0 then n_lc=posn(self$,";",1,nt)
  965     If n_lc>0 then goto nex_test
  970     If flag then call print1("end")
  980     Close all
  990     Pwrslf=flag
 1000     Return
 1010     End
 1100 Sub print1(out$)
 1110   !
 1120     Print "SELFTEST "&out$
 1130     Print #"PRIN:":"SELFTEST "&out$
 1140     Return
 1150     End
*