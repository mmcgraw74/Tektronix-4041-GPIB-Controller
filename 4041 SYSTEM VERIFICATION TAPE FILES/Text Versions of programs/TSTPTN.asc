  100 Sub tstptn local abrt,get_dev,device$,addr,lng,com0,gpb0,frtp,prn,com1,gpb1,dev$,bld_pat,patrn$,c,i,repeat,done,do_test
  110   !
  120   !  ***************************************
  130   !  * ROTATING CHARACTER SET TEST PATTERN *
  140   !  ***************************************
  150   !
  160   ! Initialization
  170   !
  180     Dim device$ to 1,dev$ to 50
  190     Integer addr,lng,c,i
  200     On abort then gosub abrt
  210     Print "TEST PATTERN"
  220   !
  230 Get_dev:   !
  240     Input prompt "DEVICE OPTION?":device$
  250     If device$="" then device$="3"
  260     If device$<>"2" and device$<>"6" then goto do_test
  270     Input prompt "WHAT GPIB ADDRESS? ":addr
  280 Do_test:   !
  290     Input prompt "WHAT LINE LENGTH? ":lng
  300     Goto val(device$) of com0,gpb0,frtp,prn,com1,gpb1
  310     Goto get_dev
  320   !
  330 Com0:   ! Standard RS-232 port
  340     Dev$="COMM0:"
  350     Goto bld_pat
  360 Gpb0:   ! Standard GPIB port
  370     Dev$="GPIB0(PRI="&str$(int(addr))&",EOM=<13>):"
  380     Goto bld_pat
  390 Frtp:   ! Front panel
  400     Dev$="FRTP:"
  410     Goto bld_pat
  420 Prn:   ! Thermal printer
  430     Dev$="PRIN:"
  440     Goto bld_pat
  450 Com1:   ! Optional RS-232 port
  460     Dev$="COMM1:"
  470     Goto bld_pat
  480 Gpb1:   ! Optional GPIB port
  490     Dev$="GPIB1(PRI="&str$(int(addr))&",EOM=<13>):"
  500   !
  510   ! Build test string
  520   !
  530 Bld_pat:   !
  540     Dim patrn$ to (127-32)+lng
  550     Patrn$=""
  560     C=31
  570     For i=1 to (127-32)+lng
  580       C=c+1
  590       If c>=127 then c=32
  600       Patrn$=patrn$&chr$(c)
  610       Next i
  620   !
  630   ! Send test pattern
  640   !
  650     Open #3:dev$
  660     C=1
  670 Repeat:   !
  680     Print #3:seg$(patrn$,c,lng)
  690     C=c+1
  700     If c>127-32 then c=1
  710     Goto repeat
  720   !
  730   ! End of test
  740   !
  750 Done:   !
  760     Close 3
  770     Print
  780     Print "TEST PATTERN EXIT"
  790     Return
  800 Abrt:   ! <ABORT> handler
  810     Branch done
  820     End
