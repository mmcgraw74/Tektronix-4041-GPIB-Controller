  100 Sub chkopt local abrt,done,memsize,rompak$,pd_in
  110   !
  120   ! ************************
  130   ! * OPTION CHECK UTILITY *
  140   ! ************************
  150   !
  160   ! Initialization
  170   !
  180     On abort then gosub abrt
  190     Integer memsize
  200     Open #2:"prin:"
  210   !
  220     Print "OPTION UTILITY"
  230     Print
  240     Rompak$=ask$("rompack")
  250     Pd_in=pos(";"&rompak$&";",";PD;",1)
  260     If pd_in=0 then goto pdtested
  270     Print #2:"  PD ROMs are"
  280     Print "PD ROMs are ";
  290     Append "pdtest",pdtested
  300     Print "installed correctly"
  310     Print #2:"installed correctly"
  320     Print #2:
  330 Pdtested:   !
  340     Memsize=int(ask("memory",all)/32768.0)+1
  350     If memsize=1 then memopt$="STANDARD"
  360     If memsize>1 and memsize<6 then memopt$="OPTION 2"&str$(memsize-2)
  370     If memsize>5 then memopt$=""
  380   !
  390     Print
  400     Print "CURRENT ROMPACKS: ";rompak$
  410     Print
  420     Print "MEMORY OPTION: ";memopt$;" - ";memsize*32;"K"
  430   !
  440     Print #2:"CURRENT ROMPACKS:"
  450     Print #2:rompak$
  460     Print #2:
  470     Print #2:"MEMORY OPTION:"
  480     Print #2:"  ";memopt$;" - ";memsize*32;"K"
  490   !
  500 Done:   !
  510     Close 2
  520     Print
  530     Print "OPTION UTILITY EXIT"
  540     Return
  550 Abrt:   !
  560     Print "TEST ABORTED"
  570     Branch done
  580     End
