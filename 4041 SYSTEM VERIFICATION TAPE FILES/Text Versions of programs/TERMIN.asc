  100   ! This material may not be copied or otherwise
  110   ! reproduced without consent of Tektronix, Inc.
  120   ! COPYRIGHT (c) 1982  TEKTRONIX, INC.   ALL RIGHTS RESERVED
  130   !
  140   ! **************************
  150   ! * TERMINAL CONFIGURATION *
  160   ! **************************
  170   !
  180   ! Initialization
  190   !
  200     Set console "frtp:"
  210     Compress all
  220     On error(700 to 999) then gosub globerr
  230     Dim in$ to 1,quote$ to 1
  240     Dim drv$ to 265,con$ to 265,tdrvr$ to 265
  250     Dim baurate$ to 4
  260     Dim answer$ to 1
  270     Dim end$ to 7
  280     Integer putcomcs,putcomdr
  290     Tdrvr$=ask$("console")
  300     Quote$=""""
  310     Print "TERMINAL CONFIG"
  320   !
  330 Baudres:   !
  340     Putcomcs=0
  350     Putcomdr=0
  360     Drv$="COMM("
  370     Con$="COMM("
  380     On abort then gosub abrt1
  390     Enable abort
  400   !
  410 Baudloop:   !
  420     Input prompt "BAUD? ":baurate$
  430     If baurate$="75" or baurate$="110" or baurate$="150" or baurate$="300" or baurate$="600" or baurate$="1200" or baurate$="2400" or baurate$="4800" or baurate$="9600" or baurate$="" then goto baudok
  440     Print "INVALID RESPONSE^G"
  450     Goto baudloop
  460 Baudok:   !
  470     If baurate$="" then baurate$="2400"
  480     Drv$=drv$&"BAUD="&baurate$
  490     Putcomdr=1
  500   !
  510 Ed_lab:   !
  520     If proced("EDIT(1-RASTER,0-STORAGE)? ") then goto more else goto do_ed
  530 Do_ed:   !
  540     If (putcomcs) then con$=con$&","
  550     Con$=con$&"EDIT=STORAGE"
  560     Putcomcs=1
  570 More:   !
  580     If proced("MORE CHANGES(1-YES,0-NO)? ") then goto ech_lab else goto thisisit
  590   !
  600 Ech_lab:   !
  610     If proced("ECHO(1-YES,0-NO)? ") then goto con_lab else goto do_ech
  620 Do_ech:   !
  630     If (putcomcs) then con$=con$&","
  640     Con$=con$&"ECHO=NO"
  650     Putcomcs=1
  660   !
  670 Con_lab:   !
  680     If proced("CONTROL(1-YES,0-NO)? ") then goto par_lab else goto do_con
  690 Do_con:   !
  700     If (putcomcs) then con$=con$&","
  710     Con$=con$&"CONTROL=NO"
  720     Putcomcs=1
  730   !
  740 Par_lab:   !
  750     Input prompt "PARITY(1-NO,2-ODD,3-EVE,4-LOW,5-HIGH)? ":answer$
  760     If answer$="1" or answer$="2" or answer$="3" or answer$="4" or answer$="5" or answer$="" then goto ok_par
  770     Print "INVALID RESPONSE^G"
  780     Goto par_lab
  790 Ok_par:   !
  800     If answer$="1" or answer$="" then goto sto_lab
  810     If (putcomdr) then drv$=drv$&","
  820     If answer$="2" then drv$=drv$&"PARITY=ODD"
  830     If answer$="3" then drv$=drv$&"PARITY=EVE"
  840     If answer$="4" then drv$=drv$&"PARITY=LOW"
  850     If answer$="5" then drv$=drv$&"PARITY=HIGH"
  860     Putcomdr=1
  870   !
  880 Sto_lab:   !
  890     Input prompt "STOP(1 OR 2)? ":answer$
  900     If answer$="1" or answer$="2" or answer$="" then goto ok_sto
  910     Print "INVALID RESPONSE^G"
  920     Goto sto_lab
  930 Ok_sto:   !
  940     If answer$="2" or answer$="" then goto bit_lab
  950     If (putcomdr) then drv$=drv$&","
  960     Drv$=drv$&"STOP=1"
  970     Putcomdr=1
  980   !
  990 Bit_lab:   !
 1000     Input prompt "BITS(5,6,7,8,9)? ":answer$
 1010     If answer$="" or answer$="5" or answer$="6" or answer$="7" or answer$="8" or answer$="9" then goto ok_bit
 1020     Print "INVALID RESPONSE^G"
 1030     Goto bit_lab
 1040 Ok_bit:   !
 1050     If answer$="8" or answer$="" then goto cr_lab
 1060     If (putcomdr) then drv$=drv$&","
 1070     Drv$=drv$&"BITS="&answer$
 1080     Putcomdr=1
 1090   !
 1100 Cr_lab:   !
 1110     Input prompt "CR(1-CR,2-CRLF,3-LFCR)? ":answer$
 1120     If answer$="" or answer$="1" or answer$="2" or answer$="3" then goto ok_cr
 1130     Print "INVALID RESPONSE^G"
 1140     Goto cr_lab
 1150 Ok_cr:   !
 1160     If answer$="2" or answer$="" then goto lf_lab
 1170     If (putcomcs) then con$=con$&","
 1180     If answer$="1" then con$=con$&"CR=CR"
 1190     If answer$="3" then con$=con$&"CR=LFCR"
 1200     Putcomcs=1
 1210   !
 1220 Lf_lab:   !
 1230     Input prompt "LF(1-LF,2-CRLF,3-LFCR)? ":answer$
 1240     If answer$="" or answer$="1" or answer$="2" or answer$="3" then goto ok_lf
 1250     Print "INVALID RESPONSE^G"
 1260     Goto lf_lab
 1270 Ok_lf:   !
 1280     If answer$="" or answer$="1" then goto cts_lab
 1290     If (putcomcs) then con$=con$&","
 1300     If answer$="2" then con$=con$&"LF=CRLF"
 1310     If answer$="3" then con$=con$&"LF=LFCR"
 1320     Putcomcs=1
 1330   !
 1340 Cts_lab:   !
 1350     If proced("CTS(1-ON,0-OFF)? ") then goto dcd_lab else goto do_cts
 1360 Do_cts:   !
 1370     If (putcomdr) then drv$=drv$&","
 1380     Drv$=drv$&"CTS=OFF"
 1390     Putcomdr=1
 1400   !
 1410 Dcd_lab:   !
 1420     If proced("DCD(1-ON,0-OFF)? ") then goto dsr_lab else goto do_dcd
 1430 Do_dcd:   !
 1440     If (putcomdr) then drv$=drv$&","
 1450     Drv$=drv$&"DCD=OFF"
 1460     Putcomdr=1
 1470   !
 1480 Dsr_lab:   !
 1490     If proced("DSR(1-ON,0-OFF)? ") then goto thisisit else goto do_dsr
 1500 Do_dsr:   !
 1510     If (putcomdr) then drv$=drv$&","
 1520     Drv$=drv$&"DSR=OFF"
 1530     Putcomdr=1
 1540   !
 1550 Thisisit:   !
 1560     Con$=con$&"):"
 1570     Drv$=drv$&"):"
 1580   !
 1590     On abort then gosub abrt2
 1600     On error(885) then gosub time_out
 1610   !
 1620     Set driver drv$
 1630 Termloop:   !
 1640     If not(proced("IS TERMINAL ONLINE?")) then goto termloop
 1650   !
 1660   ! Print strings to user
 1670   !
 1680     Open #2:"prin:"
 1690     Print #2:"TO CONFIGURE COMM"
 1700     Print #2:"THE FOLLOWING"
 1710     Print #2:"STATEMENTS SHOULD"
 1720     Print #2:"BE ENTERED IN YOUR"
 1730     Print #2:"AUTOLD FILE"
 1740     Print #2:
 1750     Print #2:"SET DRIVER """;drv$;""""
 1760     Print #2:"SET CONSOLE """;con$;""""
 1770     Print #2:
 1780     Print #2:"IF TERMINAL DISPLAYS"
 1790     Print #2:quote$&"ARE RESULTS OK?"&quote$
 1800     Print #2:"THEN TYPE "&quote$&"1"&quote$&" IN"
 1810     Print #2:"RESPONSE. OTHERWISE,"
 1820     Print #2:"PRESS <ABORT>."
 1830     Print #2:
 1840     Close 2
 1850   !
 1860   ! See if console is OK to use
 1870   !
 1880     Open #2:"comm(tim=15):"
 1890     Input prompt "^GARE RESULTS OK? " #2:in$
 1900     Close 2
 1910     If in$="1" then goto drv_ok
 1920     Set driver tdrvr$
 1930     Goto baudres
 1940 Drv_ok:   !
 1950     Print #"frtp:":"CONSOLE IS AT COMM0:"
 1960     Set console con$
 1970   !
 1980   ! Build config file
 1990   !
 2000     Drv$="100 set driver "&quote$&drv$&quote$
 2010     Con$="200 set console "&quote$&con$&quote$
 2020     End$="300 end"
 2030     On error(842) then gosub cnip_err
 2040     On error(843) then gosub winh_err
 2050     Open #2:"config(open=replace,size=510)"
 2060     Print #2:drv$
 2070     Print #2:con$
 2080     Print #2:end$
 2090     Close 2
 2100     Print "CONFIG FILE WRITTEN"
 2110     Goto done
 2120   !
 2130 Abrt1:   !
 2140 Redo:   !
 2150     Print
 2160     Input prompt "TERMINAL CONFIG(1-YES,0-NO)?":in$
 2170     If in$<>"" and in$<>"1" and in$<>"0" then goto redo
 2180     If in$="0" then load "sysver" else branch baudres
 2190 Abrt2:   !
 2200     Set driver tdrvr$
 2210     Print
 2220     Branch baudres
 2230 Cnip_err:   !
 2240     Print
 2250     If proced("INSERT TAPE(1-YES,0-NO)? ") then retry else branch done
 2260 Winh_err:   !
 2270     Print "WRITE PROTECTED^G"
 2280     If proced("WRITE ENABLE(1-YES,0-NO)? ") then retry else branch done
 2290 Time_out:   !
 2300     Print #"frtp:":"TIME-OUT ERROR^G"
 2310     Set driver tdrvr$
 2320     Print
 2330     Branch baudres
 2340 Globerr:   !
 2350     Print #"frtp(view=5):":val(ask$("error"));"ERROR DETECTED^G"
 2360     Branch done
 2370   !
 2380 Done:   !
 2390     Print #"frtp:":
 2400     Print "TERMINAL CONFIG EXIT"
 2410     End
 2500 Function proced(out$) local in$,disply,true,false,retrn
 2510   !
 2520   !  *********************
 2530   !  * SUBROUTINE PROCED *
 2540   !  *********************
 2550   !
 2560   ! This subroutine displays out$ to the currently defined console.  Any
 2570   ! keystroke other than the <PROCEED>, <CR>, 1, or 0 will cause the
 2580   ! message to be redisplayed.
 2590   !
 2600     Dim in$ to 1
 2610     Dim out$ to 50
 2620   !
 2630 Disply:   !
 2640     Input prompt out$:in$
 2650     If in$="" or in$="1" then goto true
 2660     If in$="0" then goto false
 2670     Print "INVALID RESPONSE^G"
 2680     Goto disply
 2690 False:   ! User says to abort or not proceed
 2700     Proced=0
 2710     Goto retrn
 2720 True:   ! User says to proceed
 2730     Proced=1
 2740   !
 2750 Retrn:   !
 2760     Print #"frtp:":
 2770     Return
 2780     End
